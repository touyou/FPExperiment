171023 藤井陽介

【実行例】
# let x = StringDictionary.add "one" 1 (StringDictionary.add "two" 2 (StringDictionary.add "three" 3 (StringDictionary.add "four" 4 (StringDictionary.add "five" 5 StringDictionary.empty))));;
val x : StringDictionary.t = <abstr>
# StringDictionary.lookup "one" x;;
- : int = 1
# StringDictionary.lookup "five" x;;
- : int = 5
# StringDictionary.lookup "five" (StringDictionary.remove "five" x);;
Exception: NotFound.

【コード】
type order = LT | EQ | GT

module type ORDERED_TYPE =
sig
  type t
  val compare : t -> t -> order
  val print : t -> unit
end

module type DICTIONARY =
  functor (T : ORDERED_TYPE) ->
    sig
      type t
      val empty     : t
      val add       : T.t -> int -> t -> t
      val remove    : T.t -> t -> t
      val lookup    : T.t -> t -> int
    end

type ('a, 'b) tree =
  | Leaf
  | Node of 'a * 'b * ('a, 'b) tree * ('a, 'b) tree
exception NotFound

module Dictionary : DICTIONARY =
  functor (T : ORDERED_TYPE) -> struct
    type t = (T.t, int) tree

    let empty = Leaf

    let rec add x y tr =
      match tr with
      | Leaf -> Node (x, y, Leaf, Leaf)
      | Node (a, b, l, r) ->
          match T.compare x a with
          | LT -> Node (a, b, (add x y l), r)
          | EQ -> Node (x, y, l, r)
          | GT -> Node (a, b, l, (add x y r))

    let rec search_min tr =
      match tr with
      | Leaf -> raise NotFound
      | Node (x, y, Leaf, _) -> (x, y)
      | Node(x, y, l, r) -> search_min l
    let rec delete_min tr =
      match tr with
      | Leaf -> tr
      | Node (x, _, Leaf, r) -> r
      | Node (x, y, l, r) -> Node (x, y, (delete_min l), r)
    let rec remove x tr =
      match tr with
      | Leaf -> tr
      | Node (a, b, l, r) ->
          match T.compare x a with
          | LT -> Node (a, b, (remove x l), r)
          | EQ ->
              if l = Leaf then r
              else if r = Leaf then l
              else
                let (mkey, mdata) = search_min r in
                Node (mkey, mdata, l, (delete_min r))
          | GT -> Node (a, b, l, (remove x r))

    let rec lookup x tr =
      match tr with
      | Leaf -> raise NotFound
      | Node (a, b, l, r) ->
          match T.compare x a with
          | LT -> lookup x l
          | EQ -> b
          | GT -> lookup x r

  end

module OrderedString =
  struct
    type t = string
    let compare x y =
      let r = Pervasives.compare x y in
      if      r > 0 then GT
      else if r < 0 then LT
      else               EQ
    let print x = print_string x;;
  end

module StringDictionary =
  Dictionary (OrderedString)

【考察】
型変数の型と、実際の型が推論においては違う扱いになるようで、どちらの型も後から決められるファンクターを作成しようとしたがうまくいかなかった。キーに二分木を応用し、計算量は削除や追加、検索もO(log n)ほどになっている。
